pkgname=custom-dunst
pkgver=1.4.1_5_g53e7f3d
pkgrel=1

pkgdesc="Customizable and lightweight notification-daemon"
url="https://dunst-project.org/"
arch=('x86_64')
license=('BSD')

depends=('libxinerama' 'libxss' 'pango' 'libnotify' 'gtk2' 'libxdg-basedir')
provides=('notification-daemon' 'dunst')
conflicts=('dunst')

source=('git+https://github.com/dunst-project/dunst' 'patch.diff')
md5sums=('SKIP' 'SKIP')


pkgver() {
	cd "$srcdir/dunst"
	git describe --tags --always --long | sed -e 's/-/_/g' -e 's/^v//'
}

prepare() {
	cd "$srcdir/dunst"
	git apply "$srcdir/patch.diff"
}

build() {
	make -C "$srcdir/dunst" X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	make -C "dunst" DESTDIR="${pkgdir}" PREFIX=/usr install
	install -Dm644 "${srcdir}/dunst/LICENSE" "${pkgdir}/usr/share/licenses/dunst/LICENSE"
}

